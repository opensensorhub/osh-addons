rootProject.name = 'osh-addons'

// include all folders with build.gradle as subprojects
def excludedProjects = [
  'sensorhub-storage-ndbc',
  'sensorhub-driver-nexrad', // TODO update driver to use newest version of osh-core
  'sensorhub-driver-usgswater', // TODO update driver to use newest version of osh-core
  'sensorhub-service-mfapi', // TODO update service to use newest version of osh-core
  'sensorhub-test'
]
FileTree subprojects = fileTree(rootDir).exclude('dist').include('**/build.gradle')
subprojects.files.each { File f ->
  File projectFolder = f.parentFile
  if (projectFolder != rootDir && !f.absolutePath.contains("ros")) {
    String projectName = ':' + projectFolder.name
    //String projectName = ':' + rootDir.toPath().relativize(projectFolder.toPath()).toString().replace('/', ':')
    //println projectName
    if (!excludedProjects.contains(projectName.substring(1))) {
      include projectName
      project(projectName).projectDir = projectFolder
    }
  }
}
