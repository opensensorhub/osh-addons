plugins {
    id "java"
    id "com.google.protobuf" version "0.9.4"
}

description = 'Meshtastic Driver'
ext.details = "Driver that utilizes a meshtastic Radio pre-configured with meshtastic Node Software to communicate with other meshtastic Radio"
version = '1.0.0'


dependencies {
    implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
    implementation "com.google.protobuf:protobuf-java:3.25.1"
    testImplementation "junit:junit:4.13.1"
    testImplementation project(':sensorhub-comm-jssc')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }

    // NEW LOCATION FOR GENERATED SOURCES
    generatedFilesBaseDir = "$projectDir/src/main/generated-sources"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.inputs.dir("${projectDir}/src/main/protobufs")
        }
    }
}

// --- CLEAN GENERATED SOURCES BEFORE GENERATION ---
tasks.named('generateProto') {
    doFirst {
        def dir = file("$projectDir/src/main/generated-sources")
        if (dir.exists()) {
            println "Cleaning old generated sources at $dir"
            dir.deleteDir()
        }
    }
}




// Allow Gradle/IntelliJ to see generated Java Code form Proto
sourceSets {
    main {
        // point Gradle to correct generated code location
        java {
            srcDir "$projectDir/src/main/generated-sources/main/java"
        }
        // tell the Java sourceSet where our proto files are; the plugin will read these
        proto {
            srcDirs = ['src/main/protobufs']
        }
    }
}

test {
    useJUnit()
}

// add info to OSGi manifest
osgi {
    manifest {
        attributes ('Bundle-Vendor': 'Botts Inc')
        attributes ('Bundle-Activator': 'com.sensorhub.impl.sensor.meshtastic.Activator')
    }
}

// add info to maven pom
ext.pom >>= {
    developers {
        developer {
            id 'BillBrown341'
            name 'Bill Brown'
            organization 'Botts Inc'
            organizationUrl ''
        }
    }
}
