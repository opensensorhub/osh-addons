description = 'ONVIF Video Camera'
ext.details = 'Driver for IP video cameras using the ONVIF standard'
version = '1.1.2'

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  embeddedImpl project(':sensorhub-driver-ffmpeg')
  embeddedImpl project(':sensorhub-driver-videocam')

  embeddedImpl 'org.apache.cxf.services.ws-discovery:cxf-services-ws-discovery-api:3.1.11'
  embeddedImpl 'org.apache.cxf.services.ws-discovery:cxf-services-ws-discovery-service:3.1.11'
  embeddedImpl 'org.apache.cxf:cxf-rt-transports-http-jetty:3.1.11'
  embeddedImpl 'org.apache.cxf:cxf-rt-bindings-soap:4.0.0'
  embeddedImpl 'jakarta.xml.soap:jakarta.xml.soap-api:2.0.1'
  embeddedImpl 'com.sun.activation:jakarta.activation:2.0.1'
  embeddedImpl 'jakarta.xml.ws:jakarta.xml.ws-api:3.0.1'
  embeddedImpl 'org.apache.cxf.xjc-utils:cxf-xjc-runtime:4.0.0'
  embeddedImpl 'jakarta.activation:jakarta.activation-api:2.1.3'
  embeddedImpl 'jakarta.jms:jakarta.jms-api:3.1.0'
  embeddedImpl 'jakarta.jws:jakarta.jws-api:3.0.0'
  embeddedImpl 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'
  embeddedImpl 'javax.xml.bind:jaxb-api:2.3.1'
  embeddedImpl 'org.apache.cxf.services.wsn:cxf-services-wsn-core:4.0.0'
  embeddedImpl 'org.apache.cxf:cxf-rt-frontend-jaxws:4.0.0'

  embeddedImpl('net.sf.jipcam:jipcam:0.9.1') {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-httpclient', module: 'commons-httpclient'
  }

  embeddedImpl fileTree(dir: 'lib', include: ['*.jar'])
}

tasks.register('copyReadme', Copy) {
  from "${projectDir}/README.md"
  into "${projectDir}/src/main/resources/org/sensorhub/impl/sensor/onvif"
  onlyIf { file("${projectDir}/README.md").exists() }
}

processResources {
  dependsOn copyReadme
}


// exclude tests requiring connection to the sensor
// these have to be run manually

test {
  exclude '**/TestOnvifCameraDriver.class'
}

// add info to OSGi manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Botts Innovative Research, Inc.')
    attributes('Bundle-Activator': 'org.sensorhub.impl.sensor.onvif.Activator')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'theastrowolfe'
      name 'Joshua Wolfe'
      organization 'Botts Innovative Research, Inc.'
      organizationUrl 'http://www.botts-inc.com' 
    }
  }
}
