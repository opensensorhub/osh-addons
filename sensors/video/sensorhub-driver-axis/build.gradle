description = 'Axis Video Camera'
ext.details = 'Driver for IP video cameras from Axis (with PTZ tasking support)'
version = '1.0.0'

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  implementation project(':sensorhub-driver-rtpcam')
  implementation('net.sf.jipcam:jipcam:0.9.1') {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-httpclient', module: 'commons-httpclient'
  }
  testImplementation project(path: ':sensorhub-driver-videocam', configuration: 'testArtifacts')
}

// exclude tests requiring connection to the sensor
// these have to be run manually
test {
  exclude '**/TestAxisCameraDriver.class'
}

// add info to OSGi manifest
jar {
  manifest {
    instruction 'Bundle-Vendor', 'Botts Innovative Research, Inc.'
    instruction 'Bundle-Activator', 'org.sensorhub.impl.sensor.axis.Activator'
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'mikebotts'
      name 'Mike Botts'
      organization 'Botts Innovative Research, Inc.'
      organizationUrl 'http://www.botts-inc.com' 
    }
  }
}
