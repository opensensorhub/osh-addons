description = 'Video4Linux Camera'
ext.details = 'Driver for Video4Linux compatible cameras (only supported on Linux)'
version = '1.1.0'

def thisProjectDir = projectDir
rootProject.allprojects {
  repositories {
    maven { url "file:///$thisProjectDir/libs" }
  }
}

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  implementation project(':sensorhub-driver-videocam')
  embeddedImpl 'edu.jcu:v4l4j:0.9.1'
  testImplementation project(path: ':sensorhub-driver-videocam', configuration: 'testArtifacts')
}

// exclude tests requiring connection to the sensor
// these have to be run manually
test {
  exclude '**/TestV4LCameraDriver.class'
}

// add info to OSGi manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Sensia Software LLC')
    attributes('Bundle-Activator': 'org.sensorhub.impl.sensor.v4l.Activator')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  }
}
