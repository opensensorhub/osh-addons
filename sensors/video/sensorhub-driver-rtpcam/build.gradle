description = 'RTSP/RTP Camera Driver'
ext.details = 'Sensor driver for IP cameras streaming H264 video through RTSP/RTP'
version = '1.0.0'

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  api project(':sensorhub-driver-videocam')
  testImplementation 'org.jcodec:jcodec-javase:0.1.9'
}

// exclude tests requiring connection to the sensor
// these have to be run manually
test {
  exclude '**/TestRTPCameraDriverSolo.class'
  exclude '**/TestRTPCameraDriverDahua.class'
  exclude '**/TestRTPCameraDriverAxis.class'
  exclude '**/TestRTPCameraDriverFoscam.class'
  exclude '**/TestRTPCameraDriverVirbXE.class'
}

// add info to OSGi manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Sensia Software LLC')
    attributes('Bundle-Activator': 'org.sensorhub.impl.sensor.rtpcam.Activator')
    attributes('-exportcontents': 'org.sensorhub.impl.sensor.rtpcam')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'alexrobin'
      name 'Alex Robin'
      organization 'Sensia Software LLC'
      organizationUrl 'http://www.sensiasoftware.com' 
    }
  }
}
