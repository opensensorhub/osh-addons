import org.ros.internal.message.GenerateInterfaces

// Based on work by Spyros Koukas
// https://github.com/SpyrosKou/Custom-Messages-ROS-Java-System-Example/blob/main/custom-ros-messages/build.gradle
buildscript {
    dependencies {
        classpath "org.ros.rosjava_core:rosjava:0.3.6"
        classpath "org.ros.rosjava_bootstrap:message_generation:0.3.3"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://raw.githubusercontent.com/rosjava/rosjava_mvn_repo/master/"
        }
        maven {
            url "https://artifacts.camunda.com/artifactory/public/"
        }
    }
}

plugins {
    id 'java-library'
    id 'idea'
}
ext {
    targetDir = file("src/generated-sources/java")
}

task generateRosMessages {
    group 'build'

    def resourcesFolder = "src/main/resources/";

    def stationHandlersMsgsPackageName = "transbot_msgs"
    def stationHandlerMsgsSourceDir = file(resourcesFolder + stationHandlersMsgsPackageName)

    def List<File> sources = new ArrayList();
    sources.add(stationHandlerMsgsSourceDir);

    def List<String> packages = new ArrayList();
    packages.add(stationHandlersMsgsPackageName);

    doLast {
        targetDir.mkdirs()
        def GenerateInterfaces interfacesGenerator = new GenerateInterfaces();
        interfacesGenerator.generate(targetDir, packages, sources);
    }
}

description = 'Transbot Messages'
ext.details = 'Messages Generated from Transbot Message IDL'

sourceSets.main.java.srcDirs "src/generated-sources/java"
compileJava.dependsOn generateRosMessages

repositories {
    repositories {
        mavenCentral()
        maven {
            url "https://raw.githubusercontent.com/rosjava/rosjava_mvn_repo/master/"
        }
        maven {
            url "https://artifacts.camunda.com/artifactory/public/"
        }
    }
}

group 'org.sensorhub.yahboom.transbot'
version = '1.0.0'

dependencies {
    implementation project(':sensorhub-ros-common')
    testImplementation 'junit:junit:4.12'
}

clean {
    delete fileTree(targetDir) {
        include '**/*.*'
    }
}

// add info to OSGi manifest
osgi {
    manifest {
        attributes('Bundle-Vendor': 'Botts Inc')
    }
}

// add info to maven pom
ext.pom >>= {
    developers {
        developer {
            id 'ngaray'
            name 'Nicolas Garay'
            organization 'Botts Innovative Research, Inc.'
            organizationUrl 'www.botts-inc.com'
        }
    }
}
