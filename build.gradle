apply from: gradle.oshCoreDir + '/buildSrc/common.gradle'
version = oshCoreVersion


subprojects {
  dependencies {
    compile 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  }
  
  // add project dependency to proper configuration depending on version
  // this is used to build different zip distributions
  afterEvaluate { p ->
    rootProject.dependencies {
      def dep = p.version.endsWith('SNAPSHOT') ? dev(p) : stable(p)
      dep.exclude group: 'org.sensorhub'
      dep.exclude group: 'org.vast.opengis'
    }
  }
}


// create one configuration for each distribution
configurations {
  stable
  dev
}
 
  
// distributions content
distributions {
  dev {
    baseName = 'osh-persistence-dev'
    contents {
      into('lib') {
        from project.configurations.dev
      } 
    }
  }

  stable {
    baseName = 'osh-persistence-stable'
    contents {
      into('lib') {
        from project.configurations.stable
      } 
    }
  }
}
